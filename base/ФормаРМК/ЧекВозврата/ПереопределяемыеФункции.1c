
&НаКлиенте
&После("ПриОткрытии")
Процедура Saby_ПриОткрытииПосле(Отказ)
	Saby_Данные		= Saby_Сервисы_Клиент.Saby_ИнициализацияПеременных();
	Saby_Настройки	= ЭтаФорма.ВладелецФормы.Saby_Настройки;
	context_params	= ЭтаФорма.ВладелецФормы.context_params;
	Saby_Карта		= Неопределено;
	Saby_Данные.Вставить("ДокументЭтоВозврат",			Истина);
	Saby_Данные.Вставить("ДокументОснованиеСсылка",	Объект.ЧекККМ);
КонецПроцедуры

&НаКлиенте
&Вместо("ЗаписатьФискальнуюОперациюНаКлиенте")
Функция Saby_ЗаписатьФискальнуюОперациюНаКлиенте(ТребуетсяПовторнаяПопыткаЗаписи, РеквизитыФискальнойОперацииКассовогоУзла)
	Результат = ПродолжитьВызов(ТребуетсяПовторнаяПопыткаЗаписи, РеквизитыФискальнойОперацииКассовогоУзла);
	Если Результат Тогда
		ТоварыЧекаВозврата	= Saby_ПолучитьМассивТоваровЧекаВозврата();
		
		//Для чека возврата укажем Номер применённой карты, взяв его у документа основания
		Saby_Данные.КЛНомер		= Saby_СвойстваИДопРеквизиты_Сервер.ПолучитьЗначениеСвойства(Объект.ЧекККМ, "Saby_КартаЛояльности");
		Saby_СвойстваИДопРеквизиты_Сервер.УстановитьЗначениеСвойства(Объект.Ссылка, "Saby_КартаЛояльности",	Saby_Данные.КЛНомер);
		Saby_СвойстваИДопРеквизиты_Сервер.УстановитьЗначениеСвойства(Объект.Ссылка, "Saby_ЧекККМУид", Saby_Данные.ЧекИд);
		
		Оповестить("Запись_ЧекККМВозврат_ЧекПробит", Новый Структура("ТоварыЧека, Saby_Данные", ТоварыЧекаВозврата,  Saby_Данные), Объект.Ссылка);
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаКлиенте
&После("ЗавершениеОплаты")
Процедура Saby_ЗавершениеОплаты(РезультатОплаты, ДополнительныеПараметры) Экспорт
	Если РезультатОплаты = "Отмена" Или ТипЗнч(РезультатОплаты) <> Тип("Структура") Тогда
		Saby_Карта = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
&Вместо("СмешаннаяОплата")
Процедура Saby_СмешаннаяОплата(Команда)
	Если Saby_Данные.ДокументОснованиеСсылка = Неопределено И Saby_Карта = Неопределено Тогда
		Saby_ВыполняемаяКоманда = Команда;
		ОтрытьФормуВыбораКартыЛояльности();
	Иначе
		ПродолжитьВызов(Команда);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
&Вместо("ОплатитьНаличными")
Процедура Saby_ОплатитьНаличными(Команда)
	Если Saby_Данные.ДокументОснованиеСсылка = Неопределено И Saby_Карта = Неопределено Тогда
		ОтрытьФормуВыбораКартыЛояльности();
		Saby_ВыполняемаяКоманда = Команда;
	Иначе
		ПродолжитьВызов(Команда);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
&Вместо("ОплатитьПлатежнойКартой")
Процедура Saby_ОплатитьПлатежнойКартой(Команда)
	Если Saby_Карта = Неопределено Тогда
		ОтрытьФормуВыбораКартыЛояльности();
		Saby_ВыполняемаяКоманда = Команда;
	Иначе
		ПродолжитьВызов(Команда);
	КонецЕсли;
КонецПроцедуры

